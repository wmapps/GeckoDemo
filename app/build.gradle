apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.example.geckodemo"
        minSdkVersion 21 // Cannot be smaller than version 21 for arm64
        targetSdkVersion 29
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // Only required for minSdkVersion < 24
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // You must configure a separate build variant for each CPU architecture
    flavorDimensions "abi"

    productFlavors {
        x86 { dimension "abi" }
        x86_64 { dimension "abi" }
        arm { dimension "abi" }
        aarch64 { dimension "abi" }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'

    // Add GeckoView Implementations
    x86Implementation("org.mozilla.geckoview:geckoview-${geckoviewChannel}-x86:${geckoviewVersion}", {
        exclude group: 'com.android.support'
    })
    x86_64Implementation("org.mozilla.geckoview:geckoview-${geckoviewChannel}-x86_64:${geckoviewVersion}", {
        exclude group: 'com.android.support'
    })
    armImplementation("org.mozilla.geckoview:geckoview-${geckoviewChannel}-armeabi-v7a:${geckoviewVersion}", {
        exclude group: 'com.android.support'
    })
    aarch64Implementation("org.mozilla.geckoview:geckoview-${geckoviewChannel}-arm64-v8a:${geckoviewVersion}", {
        exclude group: 'com.android.support'
    })
}
